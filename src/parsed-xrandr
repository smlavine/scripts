#!/bin/sh
# Copyright (c) 2023 Sebastian LaVine <mail@smlavine.com>
# SPDX-License-Identifier: MPL-2.0
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
# parsed-xrandr
#	Parses xrandr, and for every input xrandr detects,
#	prints the following (fields separated by tabs):
#
#		<input-name> <"connected" if connected|"active" if connected and active|"disconnected" if disconnected but still detected> <resolution if active>

xrandr | awk '
	BEGIN {
		current_connected = ""
	}

	$2 == "disconnected" {
		if (current_connected) {
			# A resolution was not found; the input is not active.
			print current_connected, "connected"
		}
		current_connected = ""
		print $1, $2
		next
	}

	$2 == "connected" {
		if (current_connected) {
			# A resolution was not found; the input is not active.
			print current_connected, "connected"
		}
		current_connected = $1
		next
	}

	current_connected && /\*/ {
		print current_connected, "active", $1
		current_connected = ""
		next
	}

	END {
		if (current_connected) {
			# A resolution was not found; the input is not active.
			print current_connected, "connected"
		}
	}
'
